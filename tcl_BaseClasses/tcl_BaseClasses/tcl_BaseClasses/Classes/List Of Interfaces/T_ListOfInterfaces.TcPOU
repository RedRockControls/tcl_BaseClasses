<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="T_ListOfInterfaces" Id="{8cd93532-dbda-456f-bbd3-f5447d66716c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK T_ListOfInterfaces IMPLEMENTS I_ListOfInterfaces
VAR
    {attribute 'analysis' := '-33'} // inhibit unused variable warning
    DynamicMemoryManager : FB_DynMem_Manager;
    Buffer               : FB_DynMem_Buffer(ipMemMan := DynamicMemoryManager);
    pInterface           : POINTER TO I_ListItem;
    _Count               : DINT;
    Capacity             : DINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{88ea0914-3836-4c42-892b-eaef76e9b52e}" />
    <Folder Name="Properties" Id="{ebcc07c5-4fc8-427f-8482-a63a96860394}" />
    <Method Name="AddInterface" Id="{04e8f8ff-97bb-4947-a245-a88e84e218b0}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddInterface
VAR_INPUT
    InterfacePointer : I_ListItem;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _Count = Capacity THEN
    Capacity := Capacity * 2;
    Buffer.Resize(nSize     := DINT_TO_UDINT(Capacity) * SIZEOF(PVOID),
                  bPreserve := TRUE,
                  bReset    := FALSE);

    pInterface := Buffer.pBuffer;
END_IF

IF pInterface <> 0 THEN
    pInterface[_Count] := InterfacePointer;
    _Count             := _Count + 1;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Clear" Id="{d4e68e9b-3933-4a21-b4ed-115dfe15e4b4}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD Clear
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pInterface = 0 THEN
    // Buffer was not allocated during Fb_init. Try again...
    Buffer.CreateBuffer(nSize  := DINT_TO_UDINT(Capacity) * SIZEOF(PVOID),
                        bReset := TRUE);
END_IF

Buffer.Clear();
_Count := 0;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="Count" Id="{f3aa3992-000d-4243-b14f-21c2091560fe}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY Count : DINT
]]></Declaration>
      <Get Name="Get" Id="{cefe473b-73da-464e-a44d-81f306694416}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Count := _Count;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_exit" Id="{d0615ceb-4945-4525-81d7-cfcbca48ccb3}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
    bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInCopyCode THEN
    Buffer.DeleteBuffer();
    pInterface := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{ffb00d3f-d993-4458-9c94-41086f494ca9}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode  : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    Capacity     : DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Buffer.CreateBuffer(nSize  := DINT_TO_UDINT(Capacity) * SIZEOF(PVOID),
                    bReset := TRUE);

pInterface := Buffer.pBuffer;

THIS^.Capacity := Capacity;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetInterface" Id="{8a10ef65-587b-4d24-ac48-fb5db0c4b005}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD GetInterface : I_ListItem
VAR_INPUT
    Index : DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Index < _Count AND Index >= 0 THEN
    GetInterface := pInterface[Index];
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>